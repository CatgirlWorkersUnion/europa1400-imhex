#include <std/io.pat>
#include <std/mem.pat>
#include <std/string.pat>
#pragma pattern_limit 999999

struct SkipRequired<auto Value, auto Padding> {
    char read_value[std::string::length(Value)] [[no_unique_address]];

    if (read_value == Value) {
        padding[Padding];
    } else {
        std::error(std::format("Could not find required value {:?} of size {} at 0x{:x}!", Value, Padding, $));
    }
} [[sealed, hidden, no_unique_address]];

struct SkipOptional<auto Value, auto Padding> {
    char read_value[std::string::length(Value)] [[no_unique_address]];
    bool skipped = true;

    if (read_value == Value) {
        padding[Padding];
        skipped = false;
    }

} [[sealed, hidden, no_unique_address]];

struct Header {
    char magic[];

    SkipRequired<"\x30", 1>;
    u32 fsize_less_10;

    SkipRequired<"\x01", 1>;
    u16 a;

    SkipRequired<"\xCD\xAB\x23", 3>;
    u32 num_keys;

    SkipOptional<"\x33\x01", 2>;
    SkipRequired<"\x24\x00", 2>;

    bool has_37 = std::mem::read_unsigned($, 1) == 0x37;
    if (has_37) {
        padding[1];
        u32 b;
    }
    bool has_36 = std::mem::read_unsigned($, 1) == 0x36;
    if (has_36) {
        padding[1];
        u32 groups_per_key;
    }
    bool has_34 = std::mem::read_unsigned($, 1) == 0x34;
    if (has_34) {
        padding[1];
        u32 num_points;
    }
    bool has_29 = std::mem::read_unsigned($, 1) == 0x29;
    if (has_29) {
        padding[1];
        u32 d;
    }
    bool has_2a = std::mem::read_unsigned($, 1) == 0x2A;
    if (has_2a) {
        padding[1];
        u32 e;
    }
};

struct Point {
    float a;
    float b;
    float c;
};

struct Skeleton {
    if (std::mem::read_unsigned($, 1) == 0x38) {
        padding[1];

        char name[];

        SkipOptional<"\x39", 1> opt_39;

        if (!opt_39.skipped) {
            Point a;

            SkipRequired<"\x3A", 1>;

            Point b;
        }
    }
};

struct PointContainer {
    SkipRequired<"\x18", 1>;
    u32 id;
    SkipRequired<"\x19", 1>;
    u32 count;
    SkipRequired<"\x21", 1>;
    Point points_list[count];
    SkipRequired<"\x28", 1>;

    SkipOptional<"\x31", 1> opt_31;
    if (!opt_31.skipped) {
        Point points_31[2];
        Skeleton skeletons[3];
    }
    std::print(std::format("ID {} count {}", id, count));
};


struct Object {

    SkipOptional<"\x31", 1> has_two_points;
    if (!has_two_points.skipped) {
        Point two_points[2];
    }

    PointContainer point_container;
};

struct Key {
    // Jedes dieser Unterobjekte entspricht einer group in der zugeh√∂rigen obj file
    // Erkennbar ist dies an der passenden Anzahl Punkte pro Group

    u32 groups_per_key = 1;

    if (parent.parent.header.has_36) {
        groups_per_key = parent.parent.header.groups_per_key;
    }

    PointContainer points[groups_per_key];
};


struct Body {
    Key keys[parent.header.num_keys];
};

struct Baf {
    Header header;
    Body body;
};

Baf baf @ $;
